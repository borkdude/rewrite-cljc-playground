= Introduction
:cljdoc-api-url: /d/clj-commons/rewrite-cljc/CURRENT/api
:toc:

== Introduction
rewrite-cljc is a library that reads and writes Clojure, ClojureScript and https://github.com/edn-format/edn[EDN] from Clojure and ClojureScript in a whitespace and comment preserving way.

== History
@xsc created https://github.com/xsc/rewrite-clj[rewrite-clj] in 2013. Its original use was to upgrade dependencies in https://leiningen.org[lein] project.clj files. The library quickly took off in popularity and is the default choice for modifying Clojure/EDN from Clojure.

@rundis created rewrite-cljs in 2015 to match the features of rewrite-clj for ClojureScript developers. It was originally used for refactoring support in https://github.com/LightTable/LightTable[Light Table]. In January of 2019, @rundis graciously https://github.com/clj-commons/rewrite-cljs[transferred rewrite-cljs to clj-commons].

Seeing an opportunity to give back to the Clojure community, in 2019 @lread, with guidance and help from many friendly Clojurians, created rewrite-cljc under clj-commons. The goal is to provide a thoughtfully maintained feature-complete library that can be used from either Clojure or ClojureScript. While the merge was not trivial the real hard work over the years was done under the leadership of @xsc and @rundis (thanks guys!). Read link:merging-rewrite-clj-and-rewrite-cljs.adoc[Merging rewrite-clj and rewrite-cljs] for details on the merge.

== Usage
rewrite-cljc is intended to be a drop-in replacement for rewrite-clj or rewrite-cljs. There are 4 public apis:

. rewrite-clj.parser
. rewrite-clj.node
. rewrite-clj.zip
. rewrite-clj.paredit

=== Parser API
Parses Clojure/ClojureScript/EDN to custom node types. It takes advantage of https://github.com/clojure/tools.reader[clojure.tools.reader] for simple forms.
You can choose to parse one or all https://www.braveclojure.com/do-things/#Forms[forms] from a string or a file.footnoteref:[file,reading from a file is only available from Clojure.]

Here we parse a single form from a string:

.REPL session
[source, clojure]
----
user=> (require '[rewrite-clj.parser :as p])
nil
user=> (def form (p/parse-string "(defn my-function [a]\n  (* a 3))"))
#'user/form
user=> form
<list:
  (defn my-function [a]
    (* a 3))
>
----

TIP: Depending on your needs, you can opt to skip the parser api and instead use zip api `of-string` or `of-file`.

See https:///d/lread/rewrite-cljs-playground/CURRENT/api/rewrite-clj.node[current parser API docs].

=== Node API
Inspect and analyse parsed nodes.

Continueing from the form we parsed above:

.REPL session continued
[source, clojure]
----
user=> (require '[rewrite-clj.node :as n])
nil
user=> (n/tag form) // <1>
:list
user=> (n/children form)
(<token: defn> <whitespace: " "> <token: my-function> <whitespace: " "> <vector: [a]> <newline: "\n"> <whitespace: "  "> <list: (* a 3)>)
user=> (n/sexpr form)
(defn my-function [a] (* a 3))
user=> (n/child-sexprs form)
(defn my-function [a] (* a 3))

user=> (n/string form) // <2>
"(defn my-function [a]\n  (* a 3))"

user=> (n/coerce '[a b c]) // <3>
<vector: [a b c]>

user=> (n/meta-node  // <4>
  (n/token-node :private)
  (n/token-node 'sym))
<meta: ^:private sym>
----
<1> dig around a bit
<2> convert the parsed nodes back to a printable string
<3> create a node from nearly any value using `coerce`
<4> nodes can be created by hand

See link:{cljdoc-api-url}/rewrite-clj.node[current node API docs].

=== Zip API
Traverse and modify Clojure/ClojureScript/EDN.

[TIP]
====
rewrite-clj uses a customized version of the https://clojure.github.io/clojure/clojure.zip-api.html[clojure.zip]. If you are not familiar with zippers, you may find the following resources helpful:

* https://clojure.org/reference/other_libraries#_zippers_functional_tree_editing_clojure_zip[Clojure overview of zippers]
* https://lambdaisland.com/blog/2018-11-26-art-tree-shaping-clojure-zip[Arne Brasseur - The Art of Tree Shaping with Clojure Zippers]
* https://tbaldridge.pivotshare.com/media/zippers-episode-1/11348/feature?t=0[Tim Baldrige - PivotShare - Series of 7 Videos on Clojure Zippers]
====

.REPL session
[source, clojure]
----
user=> (require '[rewrite-clj.zip :as z])
nil
user=> (def data-string
"(defn my-function [a]
  ;; a comment
  (* a 3))")
#'user/data-string
user=> (def data (z/of-string data-string))  // <1>
#'user/data

user=> (z/sexpr data)  // <2>
(defn my-function [a] (* a 3))
user=> (-> data z/down z/right z/node)
<token: my-function>
user=> (-> data z/down z/right z/sexpr)
my-function

user=> (-> data z/down z/right (z/edit (comp symbol str) "2") z/up z/sexpr) // <3>
(defn my-function2 [a] (* a 3))

user=> (-> data z/down z/right (z/edit (comp symbol str) "2") z/print-root)
(defn my-function2 [a]
  ;; a comment
  (* a 3))nil
----
<1> use `of-string` to parse and zip in one step
<2> explore a bit
<3> make some changes. `rewrite-clj.zip/edit` and `rewrite-clj.zip/replace` transparently coerce between the node representation (`<token: my-function>`) and its corresponding s-expression (`my-function`).

TIP: The zip location movement functions (`right`, `left`, `up`, `down`, etc) skip over whitespace nodes. If you want to see all nodes, use the * counterparts (`right*`, `left*`, `up*`, `down*`, etc).

[CAUTION]
====
Use rewrite.zip/sexpr with care:

* Not everything can be converted into an s-expression.
* Be mindful of language differences between Clojure and ClojureScript.
====

See link:{cljdoc-api-url}/rewrite-clj.zip[current zip API docs].

==== Sweet Code Traversal with the Zip API

rewrite-clj.zip includes find operations to navigate to locations of interest. Let's assume you want to modify the following `project.clj` by replacing the `:description` placeholder text with something more meaningful:

.project.clj snippet
[source, clojure]
----
(defproject my-project "0.1.0-SNAPSHOT"
  :description "Enter description"
  ...)
----

Most find functions accept an optional location movement function. To perform a depth-first search use `rewrite-clj.zip/next`, to search sibling nodes to the right, use `rewrite-clj.zip/right` (the default), to search siblings to left, use `rewrite-clj.zip/left`.

.REPL session
[source, clojure]
----
user=> (require '[rewrite-clj.zip :as z])
nil
user=> (def data (z/of-file "project.clj")) // <1>
#'user/data
user=> (def prj-map (z/find-value data z/next 'defproject)) // <2>
#'user/prj-map

user=> (def descr (-> prj-map (z/find-value :description) z/right)) // <3>
#'user/descr
user=> (z/sexpr descr)
"Enter description"

user=> (-> descr (z/replace "My first Project.") z/print-root) // <4>
(defproject my-project "0.1.0-SNAPSHOT"
  :description "My first Project."
  ...)
nil

----
<1> load the `project.clj` file.footnoteref:[file]
<2> navigate depth-first to the project map by searching for `'defproject`.
<3> navigate to the description text by searching right for `:description` and then moving one node to the right. Remember that while whitespace is preserved it is automatically skipped during navigation.
<4> replace the description, zip up and print the result.

==== Handling Clojure Data Structures with the Zip API

The zip api provides familiar ways to work with Clojure data structures. It offers functions that correspond to the standard seq functions, for example:

.REPL session
[source, clojure]
----
user=> (require '[rewrite-clj.zip :as z])
nil
user=> (def data (z/of-string "[1\n2\n3]"))
#'user/data
user=> (z/vector? data)
true
user=> (z/sexpr data)
[1 2 3]
user=> (-> data (z/get 1) z/node)
<token: 2>
user=> (-> data (z/assoc 1 5) z/sexpr)
[1 5 3]
user=> (->> data (z/map #(z/edit % + 4)) z/->root-string)
"[5\n6\n7]"
----

==== Tracking Position with the Zip API
TODO: an example


=== Paredit API
Structured editing was introduce by rewrite-cljs.

See link:{cljdoc-api-url}/rewrite-clj.paredit[current paredit API docs].
